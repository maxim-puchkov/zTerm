#!/bin/zsh

#  update-service
#  com.maximpuchkov.services.Update
#
#  Created by Maxim Puchkov on 2020-04-30.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


log_prefix="$(date -j +'%b %d %T') $(hostname -s) ${0:t}[$$]:\t"
echo "${log_prefix} Service started."


function test_command() {
    command_name="$1"
    if ! whence ${(P)command_name} &>/dev/null; then
        echo "${log_prefix} [127] Command not found: ${command_name:l}." >&2
        exit 127
    fi
    return 0
}


# Input
BREW=$1
test_command BREW
TLDR=$2
test_command TLDR

# Before update
brew_before=${#$( $BREW outdated --quiet )}
tldr_before=${#$( $TLDR --list )}
error=0


#MARK: - Update
( # Update Homebrew formulae
  echo "Updating Homebrew..."
  $BREW update
  echo "Upgrading Homebrew formulae..."
  $BREW upgrade
) && (
  # Update tldr
  echo "Updating TLDR..."
  $TLDR --update
) || error=$?


if [[ $error -eq 0 ]]; then
  # After update
  brew_after=${#$( $BREW outdated --quiet )}
  tldr_after=${#$( $TLDR --list )}
  # Difference
  brew_diff=$(( $brew_before - $brew_after ))
  tldr_diff=$(( $tldr_after - $tldr_before ))
  # Log the number of updated formulae/commands
  echo "${log_prefix}[0] Updated ${brew_diff} Homebrew formulae and added ${tldr_diff} TLDR commands."
else
  # Log error
  echo "${log_prefix}[${error}] Update failed." >&2
fi


echo "${log_prefix} Service exited with code ${error}."
exit $error
