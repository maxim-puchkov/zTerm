#!/bin/zsh

#  system-appearance-service
#  Services
#
#  Created by Maxim Puchkov on 2020-04-25.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


#MARK: - Configuration Files
ROOT_DIR="${ZTERM:-/usr/local/Terminal}"
# System dark appearance description
typeset -r appearances_file="${ROOT_DIR}/etc/SystemAppearance/appearances"
typeset -A dark_appearance
dark_appearance=($(< $appearances_file))
# Start and End hour of dark appearance
typeset -r intervals_file="${ROOT_DIR}/etc/SystemAppearance/intervals"
typeset -A time_intervals
time_intervals=($(< $intervals_file))


#MARK: - Time
# Determine Start, End, and Current times
begin_time=$( date -v "${time_intervals[begin]}H" -j +'%s' )
end_time=$( date -v "${time_intervals[end]}H" -j +'%s' )
current_time=$(date -j +'%s')
# Compare the times time and set the New Status
if [[ $current_time -ge $begin_time ]] &&
    [[ $current_time -lt $end_time ]]; then
    new_status='off'
else
    new_status='on'
fi


#MARK: - Logging
#
log_prefix="$(date -r $current_time -j +'%b %d %T') $(hostname -s) ${0:t}[$$]:"
log_info="${log_prefix} [0] system appearance changed to ${dark_appearance[$new_status]}."
log_error="${log_prefix} [1] ${dark_appearance[$new_status]} appearance is already set."


#MARK: -
#
current_status=$( /usr/local/bin/dark-mode status )
if [[ "$new_status" == "$current_status" ]]; then
    echo "$log_error" >&2
    exit 1
fi
#
echo "$log_info"
/usr/local/bin/dark-mode "$new_status" &> /dev/null
exit 0
