#!/bin/zsh

#  save_array
#  Z-Function
#
#  Created by Maxim Puchkov on 2020-04-08.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Save an array to file.
function save_array() {
    # Variable name of the array to save
    local var_id=$1
    if [[ -z $var_id || -z ${(P)var_id} ]]; then
        error "'$(Bad $var_id)' is not a valid identifier."
        return 2
    fi
    
    # Convert associative arrays
    typeset -a arr
    local var_type=${(Pt)var_id}
    case $var_type in
        *'association'*)
            arr=(${(Pkv)var_id})
            ;;
        *'array'*)
            arr=(${(P)var_id})
            ;;
        *)
            error 'Invalid type.'
            return 2
            ;;
    esac
    
    # Check type
    if [[ ! ${(t)arr} =~ 'array' ]]; then
        error "$(Bad var_id) is not an array."
        return 2
    fi
    # Check null values
    if [[ ! $(( ${(Pk)#var_id} * 2 )) -eq ${#arr} ]]; then
        error "$(Bad var_id) invalid association."
        return 2
    fi
    
    # File to which the array will be written to
    local default_filepath="$DEV/Library/Data/$var_id.array"
    local filepath="${${2:a}:-$default_filepath}"
    if [[ -d "$filepath" ]]; then
        filepath="${filepath}/${var_id}"
    fi
    
    # Write to file
    if (! print -r -- ${(qq)arr} > "$filepath"); then
        return 1
    fi
    return 0
}
