#!/bin/zsh

#  applescript.zsh
#  Generated by mkzsh at 07:21pm.
#
#  Created by mpuchkov/506:20 on April 06, 2021.
#  ~/private/zsh/lib/applescript.zsh


autoload -Uz error


# NAME
#   tell
#
# SYNOPSIS
#   tell <commands>
#
# EXAMPLES
#   tell 'application "System Events"' 'return its properties'
#   tell 'application "Finder"' 'return POSIX path of (insertion location as alias)'
function tell() {
  local -a tell_cmds=( "tell ${1}"  ${(@F)argv:2}  "end tell" )
  local tell_out="${(Q)$( command osascript -s es       \
                          <<<${(@F)tell_cmds}     \
                          2> >(error -I -d: -f2-) )}"
  if [[ $status -eq 0  ]] \
  && [[ -n "$tell_out" ]]; then
    printf '%s\n' "$tell_out"
  fi
}




## Xcode ##

# FUNCTION
#   xc-source: Source the document which is currently opened in Xcode.
function xc-source() {
  local xcode_document="$(xc-document)"
  if [[ $? -eq 0 ]] && [[ -r $xcode_document ]]; then
    print -u2 -P -- "%F{3}[sourcing %U$xcode_document%u]%f"
    source $xcode_document
  fi
}

# FUNCTION
#   xc-document: Return path of the document opened in Xcode.
function xc-document() {
  printf '%s\n' "$( tell 'script "Xcode"' 'return get_opened_document_path()' )"
}




## System Events ##
# NAME
#   ps-names
#
# DESCRIPTION
#   List names of all running apps and processes.
function ps-names() {
  typeset -a running_processes
  running_processes=( $(tell 'script "System Events"' 'return list_process_names()') )
  print -nr -oi -C1 -- ${running_processes}
}




## Google Chrome ##
# Open new window with url.
function chrome_make_new_window() {
  local url="${*// /+}"
  tell 'script "Google Chrome"' 'make_new_window("normal", "'"$url"'")'
}

# Move front tab in a normal window to a new incognito window.
function chrome_move_tab_to_incognito() {
  tell 'script "Google Chrome"' 'move_tab_to_incognito()'
}

# Close chrome windows if more than 'max_windows' windows are opened.
function chrome_limit_windows() {
  local max_windows="${*:-5}"
  tell 'script "Google Chrome"' 'limit_windows('"$max_windows"')'
}




## Aliases ##
alias tell-xcode="tell 'script \"Xcode\"'"
alias tell-chrome="tell 'script \"Google Chrome\"'"
alias tell-sysevents="tell 'script \"System Events\"'"
