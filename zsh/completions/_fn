#compdef fn

#  _fn
#  Generated by mkzsh at 11:08pm.
#
#  Created by mpuchkov/506:20 on April 11, 2021.
#  ~/.zsh/completions/_fn


function _fn() {
local -a cmds subcmds
cmds=(
  'changelog:Print the changelog'
  'help:Usage information'
  'plugin:Manage plugins'
  'pr:Manage Oh My Zsh Pull Requests'
  'theme:Manage themes'
  'update:Update Oh My Zsh'
)

if (( CURRENT == 2 )); then
  _describe 'command' cmds
elif (( CURRENT == 3 )); then
  case "$words[2]" in
    plugin) subcmds=('info:Get plugin information' 'list:List plugins')
      _describe 'command' subcmds ;;
    pr) subcmds=('test:Test a Pull Request' 'clean:Delete all Pull Request branches')
      _describe 'command' subcmds ;;
    theme) subcmds=('use:Load a theme' 'list:List themes')
      _describe 'command' subcmds ;;
  esac
elif (( CURRENT == 4 )); then
  case "$words[2]::$words[3]" in
    plugin::info) compadd "$ZSH"/plugins/*/README.md(.N:h:t) \
      "$ZSH_CUSTOM"/plugins/*/README.md(.N:h:t) ;;
    theme::use) compadd "$ZSH"/themes/*.zsh-theme(.N:t:r) \
      "$ZSH_CUSTOM"/**/*.zsh-theme(.N:r:gs:"$ZSH_CUSTOM"/themes/:::gs:"$ZSH_CUSTOM"/:::) ;;
  esac
fi

return 0
}
