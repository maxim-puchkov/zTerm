#!/bin/zsh

#  mkzsh
#  Generated by mkzsh at 07:15pm.
#
#  Created by mpuchkov/506:20 on April 16, 2021.
#  ~Projects/zTerm2/zsh/functions/mkzsh


# NAME
#   mkzsh
#
# SYNOPSIS
#   mkzsh -[oxvws] <file> [file-description]
#
# DESCRIPTION
#   Make a zsh script.
autoload -Uz error
local -A opts
local -a specs=(
  '-open'       'o=-open'
  '-executable' 'x=-executable'
  '-overwrite'  'w=-overwrite'
  '-verbose'    'v=-verbose'
  '-sh'         's=-sh'
)
zparseopts -D -F -M -A opts - $specs
if [[ $# -eq 0 ]]; then
  error -1 -m 'not enough arguments'
fi
local file="${1:A}"
# -w: overwrite existing file.
if [[ ${+opts[--overwrite]} -eq 1 ]]; then
  if [[ ! -e "$file" ]]; then
    error -m 'file does not exist: ${file}'
  fi
  { command stat -lF $file 2>/dev/null \
    && command rm -i $file
  } || return 1
fi
# Stop if file already exists.
if [[ -e "$file" ]]; then
  error -m 'file already exists: ${file}'
fi
# Write command path and header comment to file.
local shebang="#!/bin/zsh"
if [[ ${+opts[--sh]} -eq 1 ]]; then
  shebang="#!/bin/sh"
fi
printf '%s\n\n' "$shebang" > $file
file_header $file "Generated by ${0} at %D{%I:%M%p}." >> $file
# -x: make executable.
if [[ ${+opts[--executable]} -eq 1 ]]; then
  command chmod +x "$file"
fi
# -o: open file for editing.
if [[ ${+opts[--open]} -eq 1 ]]; then
  command open -t "$file"
fi
# -v: print name of created file.
if [[ ${+opts[--verbose]} -eq 1 ]]; then
  printf '%s: created file: %s\n' "$file"
fi
