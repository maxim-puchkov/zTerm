#!/bin/zsh

#  cdp
#
#
#  Created by mpuchkov on 2020-01-30.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Change directory and print its path.
function cdp() {
    trap "unfunction -m '__${0}_*'" EXIT
    
    # Print error message
    function __cdp_error() (
        typeset program_label error_text
        program_label="%B%F{9}${program_name}%f%b"
        error_text="$@"
        print -P -- "${program_label}: ${error_text}"
    ) >&2
    
    #
    function __cdp_debug() (
        vars=(program_name optlist argv dir arg old new)
        for v in $vars; do
            print -P -- "Variable %B${v}%b = ${(P)v}"
        done
    )
    
    
    
    
    # Program data
    typeset program_name=$0
    typeset dir arg old new
    typeset -a optlist
    
    # Program options
    optlist=()
    while [[ $1 =~ '^-' ]]; do
        case $1 in
            -[qsLP])
                optlist+=($1)
            ;;
            -[0-9])
                break
            ;;
            --)
                shift
                break
            ;;
        esac
        shift
    done
    
    # Program arguments
    case $# in
        # No arguments: 'cd'
        0)  dir="$HOME"
        ;;
        # One argument: 'cd DIR'
        1)  arg="$1"
            if [[ ${arg[1]} =~ '^(\+|\-)' ]]; then
                local stack_index
                case ${arg[1]} in
                    -)  n=${${arg:1}:-1}
                        stack_index=$n
                    ;;
                    +)  n=${arg:1}
                        stack_index=$(( ${#dirstack} - $n ))
                    ;;
                esac
                dir=${dirstack[$stack_index]}
                if [[ -z "$dir" ]]; then
                    __cdp_error "no such entry in dir stack."
                fi
            else
                dir="${arg:a}"
            fi
        ;;
        # Two arguments: 'cd OLD NEW'
        2)  old="$1"
            new="$2"
            if [[ ! "$PWD" =~ "$old" ]]; then
                __cdp_error "string %F{1}${old}%f is not in %F{1}${PWD}%f."
                return 1
            fi
            dir=${PWD//$old/$new}
        ;;
        # More than two arguments
        *)  __cdp_error 'too many arguments.'
            return 2
        ;;
    esac
    
    if [[ ! -e "$dir" ]]; then
        __cdp_error "no such file or directory: ${dir:t}."
        return 1
    fi
    if [[ ! -d "$dir" ]]; then
        __cdp_error "${dir:t} is not a directory."
        return 1
    fi
    
    if [[ "$dir" != "$PWD" ]] && cd ${=optlist} "$dir"; then
        print -P "Directory changed to %F{2}$PWD%f."
    fi
    return 0
}
