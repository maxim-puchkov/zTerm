#!/bin/zsh

#  ref
#  Quick shell reload
#
#  Created by mpuchkov on 2020-04-21.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Reload modified autoloaded functions and shell files
function ref() {
    local updated_files=(${(@f)"$(
        find $ZDOTDIR \( -name '.z*' -or -regex '.*/functions/.*' \) \
             -and -newerma $ZSHRC |
        awk -F '/zsh/' '{ print $2; }'
    )"})
    
    if [[ -v REF_ZDOT ]]; then
        local updated_zdot=( ${updated_files:*REF_ZDOT} )
        local zdot
        for zdot in $updated_zdot; do
            builtin source $ZDOTDIR/$zdot
        done
    fi
    
    local updated_func=( ${(M)updated_files:#functions/*} )
    local func
    for func in ${updated_func#functions/}; do
        unfunction -m "${func}_*"
        unfunction $func
        autoload $func
    done 2>/dev/null
    
    if  [[ ${#updated_func} -gt 0 ]] ||
        [[ ${#updated_zdot} -gt 0 ]]; then
        touch $ZSHRC
    fi
    return 0
}
