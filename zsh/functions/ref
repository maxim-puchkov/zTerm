#!/bin/zsh

#  ref
#  Quick shell reload
#
#  Created by mpuchkov on 2020-04-21.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Reload modified autoloaded functions and shell files
function ref() {
    () {
        typeset -a all_updated_files=(${(@f)"$(
            find $ZDOTDIR \( -name '.z*' \
                 -or  -regex '.*/functions/.*' \) \
                 -and -newerma $ZSHRC |
            awk -F '/zsh/'  '{ print $2; }'
        )"})
        
        if [[ -v REF_ZDOT ]]; then
            typeset -a updated_zdot_files=( ${all_updated_files:*REF_ZDOT} )
            local zdot_file
            for zdot_file in $updated_zdot_files; do
                builtin source $ZDOTDIR/$zdot_file
            done
        fi
        
        typeset -a updated_funcs=( ${(M)all_updated_files:#functions/*} )
        local func
        for func in ${updated_funcs#functions/}; do
            unfunction -m "${func}_*"
            unfunction $func
            autoload   $func
        done
        
        if [[ ${#updated_zdot_files} -gt 0 ]] ||
           [[ ${#updated_funcs} -gt 0 ]]; then
            touch $ZSHRC
        fi
    } 2>/dev/null
    
    return 0
}
