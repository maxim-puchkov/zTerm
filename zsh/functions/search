#!/bin/zsh

#  search
#  zTerm (id: 157)
#
#  Generated by 'zfn' on 2020-11-13
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# search
#
# <#Description#>
#
# Usage:
# search <#...#>
emulate -LR zsh
typeset PROG="${0##*/}"
# Function options.
# @define     option
# @define     args    *: :_default
# @annotation option  @opt
#
# @opt -g,--google: search on Google.
# @opt -i,--images: search images on Google.
# @opt -d,--duckduckgo: search on DuckDuckGo.
# @opt -t,--thesaurus: search synonyms on Thesaurus.com.
# @opt -d,--dictionary: search definitions on Dictionary.com.
local -A opts
local -a specs=(
  -google       'g=-google'
  -images       'i=-images'
  -duckduckgo   'd=-duckduckgo'
  -thesaurus    't=-thesaurus'
  -dictionary   'D=-dictionary'
)
if ! zparseopts -D -E -F -K -M -A opts - ${specs[@]} \
   2> >(error -I -d: -f3-); then
  return 1
fi

# Default: search for all results on Google.
if [[ ${#opts} -eq 0 ]]; then
  opts[--google]="true"
fi


# Search query.
local query="${*// /+}"
if [[ -z "$query" ]]; then
  error -1 -m 'search query is not specified'
fi


# Search urls.
local -a urls=()
case 1 in
  $+opts[--google]) urls+="https://www.google.com/search?q=${query}"          ;|
  $+opts[--images]) urls+="https://www.google.com/search?q=${query}&tbm=isch" ;|
  $+opts[--duck])   urls+="https://duckduckgo.com/?q=${query}&t=osx"          ;|
  $+opts[--thesaurus])  urls+="https://www.thesaurus.com/browse/${query}"     ;|
  $+opts[--dictionary]) urls+="https://www.dictionary.com/browse/${query}"    ;|
esac

# Open all urls in browser.
/usr/bin/open -- "${urls[@]}"
return $?
