#!/bin/zsh

#  fstr
#  Z-Function (5)
#
#  Created by Maxim Puchkov on 2020-04-27.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Find strings (or patterns) in files.
function fstr() {
    if [[ $# -eq 0 ]]; then
        error -2 'No input.\nUsage: %B%Ufstr%u <directory> <string>%b.'
    fi
    
    # String, directory, and depth
    local search_directory="${${1:-.}:a}"
    local string="${2:-.}"
    shift 2
    
    # Search options for grep
    local -a grep_options=($argv)
    if [[ -z $grep_options ]]; then
        grep_options=('--ignore-case' '--text')
    fi
    
    # Files in the directory
    local -a files=(${(@f)"$(
        find -L $search_directory           \
                '(' -type f -or -type l ')' \
                2>/dev/null
    )"})
    local -i n_files n_matches
    n_files=${#files}
    n_matches=0
    
    # Find matching files
    local file match
    for file in $files; do
        match=$( colorize_grep 'b_byellow' \
            egrep --color='always'         \
                  $grep_options            \
                  --regexp "$string" --    \
                  $file )
        if [[ -n "$match" ]]; then
            n_matches=$(( $n_matches + 1 ))
            print -P -- "%B%K{11}%F{16} [Match $n_matches]  $file %f%k%b"
            print    -- "$match"
        fi
    done
    
    # Display number of matches and files
    logf 'String $string is found in $n_matches out of $n_files files.'
    return 0
}
