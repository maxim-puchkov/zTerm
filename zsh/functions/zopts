#!/bin/zsh

#  zopts
#  devx
#
#  Created by admin on 2020-03-02.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Show enabled/disabled options.
function zopts() {
    local ON=$(green 'on')
    local OFF=$(red 'off')
    local title='%U%F{4}Z-Shell Options%f%u'
    local k v
    typeset -A list
    list=(${(kv)options})
    local pattern='.'
    
    
    # Filter results by options which are either
    # 'on' or 'off'; or by option name.
    if [[ -n $1 ]]; then
        local filter=$1
        case $filter in
            'on'|'off')  list=(${(kv)options[(R)$filter]}) ;;
                     *)  pattern="$filter" ;;
        esac
    fi
    
    
    print -PN -- '%B'
    print -P  -- "$title\n"
    printf '%-20s %c %-9s\n' 'Option' '|' 'Status'
    printf '%-30s\n' "${(l:30::-:)}"
    print -PN -- '%b'
    
    for k v in ${(kv)list}; do
        local option_name_label=$k
        local option_value_label=$OFF
        if [[ $v == 'on' ]]; then
            option_value_label=$ON;
        fi
        if [[ $option_name_label =~ "${pattern}" ]]; then
        
        printf '%-20s %c %-9s\n' "$option_name_label" '|' "$option_value_label"
        fi
    done
}
