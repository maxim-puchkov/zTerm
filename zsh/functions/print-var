#!/bin/zsh

#  print-var
#  Z-Function (57)
#
#  Created by Maxim Puchkov on 2020-06-06.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.




# Function print-var:
#   Show type, length, value of shell variables.
#
# Options:
## print-var -t:    print variable types only
## print-var -b:    brief output
function print-var() {
    # Exclude duplicates, rename '@' to 'argv'
    set -- ${(u)argv:/@/argv}
    
    # Get options
    typeset -A optlist
    autoload -Uz get-arguments
    get-arguments -A optlist --flags t b 1 -- $argv
    
    # Raise error when variable name is not given
    typeset -i argc=$#
    if [[ $argc -eq 0 ]]; then
        error -1 'Enter at least one variable name.'
        return 1
    fi
   
    # Brief output
    if [[ $optlist[-b] -eq 1 ]]; then
        while [[ -n $1 ]]; do
            if [[ -v $1 ]]; then
                print -P -- "%B%F{4}${1}%f%b = ${(P)1}"
                notify -- "%B%F{4}${1}%f%b = ${(P)1}"
            else
                (var_id=$1  warning 'variable $var_id is not set')
            fi
            shift
        done
    else
        # Trap function to display variable in
        # the environment of the caller
        trap '(
            export prog='$0'
            # Set constant values
            typeset {null,undefined,join,array_join}=""
            null=${text_labels[null]:-null}
            undefined=${text_labels[undefined]:-undefined}
            join="%u, %U"
            array_join="\n   "
            
            # Loop for every variable ID
            typeset var_id=""
            for var_id in '"$argv"'; do
                # Check that variable is set
                if [[ ! -v $var_id ]]; then
                    error -1 "Variable \$var_id is not set."
                    continue
                fi
                
                # Format variable description
                typeset {type,length,description,desc_format}=""
                type=${(Pt)var_id}
                length=${(P)#var_id}
                description="(%Btype%b: ${type:-$undefined}, %Blength%b: ${length:-0})"
                desc_format="%F{2}${var_id}%f${description}"
                
                # Print only description if '-t' is given
                if [[ "'$optlist[-t]'" -eq 1 ]]; then
                    print -P -- "${desc_format}"
                    continue
                fi
                
                # Format variable value based on its type
                typeset __value_format=""
                if [[ -z ${(P)var_id} ]]; then
                    __value_format="${null}"
                else
                    unset __value
                    case $type in
                        # Associative arrays: [key] = value
                        *"association"*)
                            typeset -a __value=(${(Pqkv)var_id})
                            __value_format="{"
                            for index in {1..$#__value..2}; do
                                local k="${${(Q)__value[$index]}:-$null}"
                                local v="${${(Q)__value[$(( $index + 1 ))]}:-$null}"
                                __value_format+="${array_join}[${k}] = %U${v}%u"
                            done
                            __value_format+="\n}"
                        ;;
                        # Arrays: [index] = value
                        *"array"*)
                            typeset -a __value=(${(Pq)var_id})
                            __value_format="{"
                            for index in {1..$length}; do
                                __value_format+="${array_join}[${index}] = %U${${(Q)__value[$index]}:-$null}%u"
                            done
                            __value_format+="\n}"
                        ;;
                        # Integers: value
                        *"integer"*)
                            typeset __value="${(@P)var_id}"
                            __value_format="%U${__value}%u"
                        ;;
                        # Other: "value"
                        *)
                            typeset __value="${(@P)var_id}"
                            __value_format="\"%U${__value}%u\""
                        ;;
                    esac
                fi
                
                # Print variable
                print -P -- "${desc_format} = ${__value_format}"
            done
        )' EXIT
    fi
    
    return $?
}
