#!/bin/zsh

#  show
#  Z-Function (77)
#
#  Created by Maxim Puchkov on 2020-06-30.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.



# show - show information about a shell command,
#        parameter or function
[[ $# -eq 0 ]] && return 1


# For each argument, show description
local desc not_found
for arg; do
  print -Pr -- "%B%F{10}>%f ${arg}%b"
  local desc=''
  case 1 in
    ${+aliases[$arg]})
      desc+="alias for $(esc0 4,34 -t "${aliases[$arg]}")\n"
    ;|
    ${+commands[$arg]})
      desc+="command $(esc0 4,36 -t "${commands[$arg]}")\n"
    ;|
    ${+functions[$arg]})
      autoload +X $arg
      desc+="$(esc0 1,35 -t 'function') $(functions $arg | highlight)\n"
    ;|
    ${+nameddirs[$arg]})
      desc+="directory $(esc0 4,33 -t "${nameddirs[$arg]}")\n"
    ;|
    ${+parameters[$arg]})
      typeset -a arr
      set -A arr \'$'\e[92m'${(Pkv)^arg}$'\e[0m'\'
      desc+="${parameters[$arg]} $(esc0 1,4,32 -t "${arg}") = ${(kvj:, :)arr}\n"
    ;|
  esac
  not_found=$(esc0 1,91 -t "${arg}" 22,31 -t 'not found')
  print -n -- ${(%)desc:=%B$arg%b' %F{1}not found%f\n'}
done


return 0
