#!/bin/zsh

#  template
#  Documents & Support
#
#  Created by admin on 2020-03-04.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Generate files from templates.
# Synopsis:
#   template [options...] kind
function template() {
#    export SYNTAX=(template ${option}${__optlist__} kind)
    export TEMPLATES_DIR="$ZDOTDIR/templates"
    
    
    # Generate unique suffixes for files which
    # already exist in Output directory.
    #   E.g.: file, file-2, ..., file-N.
    function generate_filename() {
        # Output file.
        local name="$1"
        local base=${name:r:t}
        if [[ -z $name ]]; then
            return 1
        fi
            
        # Count all files starting with the same 'base'.
        local count=$( countf "$target_dir" "$base" prefix )
        
        # If there exists a file with a similar name,
        # save new file with appended file number.
        local suffix=''
        if [[ $count -ge 1 ]]; then
            local file_number=$(( $count + 1 ))
            suffix="-$file_number"
        fi
        
        # Applend file extension if necessary.
        local ext=''
        if [[ -n ${name:e} ]]; then
            ext=".${name:e}"
        fi
        
        # Print generated name.
        local new_name="${base}${suffix}${ext}"
        echo $new_name
    }
    
    # Replace ___MACRO___ strings.
    function substitute_macros() {
        local file=$1
        if [[ ! -e $file ]]; then
            return 1
        fi
        shift
        for macro in "$@"; do
            local macro_id="___${(U)macro#m_}___"
            local macro_value=${(P)macro}
            sed -i '' "s/$macro_id/$macro_value/g" $file
        done
    }
    
    
    #MARK: - Z-Shell Command Template
    function template_zsh() {
        local name=${$( generate_filename ${1:-${(U)0}})}
        local package=$2
        local new_template=$target_dir/$name
        local TEMPLATE_ORIG=$TEMPLATES_DIR/$0
        
        # Create new file from a copy of the original file.
         if (cp $TEMPLATE_ORIG $new_template); then
             m_filename=$name
             m_package_name=$package
             # Substitute template macro strings.
             substitute_macros $new_template $TEMPLATE_MACROS
             echo "Generated template file: $(OK $new_template)."
         fi
    }
    
    #MARK: - Z-Shell Environment Function
    function template_zfunction() {
        local name=${$( generate_filename $1 ):-${(U)0}}
        local package='Z-Function'
#        local target_dir="${zfunc}/zshell"
        local new_template="${target_dir:-${zfunc}/zshell}/${name}"
        local TEMPLATE_ORIG=$TEMPLATES_DIR/$0
        
       # Create new file from a copy of the original file.
        if (cp $TEMPLATE_ORIG $new_template); then
            m_filename=$name
            m_package_name=$package
            # Substitute template macro strings.
            substitute_macros $new_template $TEMPLATE_MACROS
            echo "Generated template file: $(OK $new_template)."
        fi
    }
    
    
    # Template macro strings.
    typeset -a TEMPLATE_MACROS=(
        m_shebang           m_template_ts
        m_filename          m_package_name
        m_owner             m_username
        m_date_full         m_date_year
        m_organization_id   m_product_id
    )
    m_shebang='\#\!\/bin\/zsh'
    m_template_ts='<#timestamp#>'
    m_filename="<#name#>"
    m_package_name="<#package#>"
    m_owner='Maxim Puchkov'
    m_username='mpuchkov'
    m_date_full=$( date +'%Y-%m-%d' )
    m_date_year=$( date +'%Y' )
    m_organization_id='com.maximpuchkov'
    export TEMPLATE_MACROS
    
    # Function options.
    typeset -A optlist
    get_options_v2 optlist 'd:D' "$@"
    shift $optlist[length]
        
    # Source and compound commands.
    src=$( basename $0 )
    cmd=$1
    
    # Template output directory (-d).
    target_dir=${${optlist[d]:-$PWD}:a}
    if [[ ! -d $target_dir ]]; then
        error 'Invalid directory.'
        return 2
    fi
    
    # Compound command.
    case $cmd in
        "")
            print "Usage:"
            print -x2 "\t$(b_bcyan $src) $(cyan 'command') $(cyan 'arguments')... - call command '$src command' with a list of 'arguments'."
            ;;
        *)
            shift
            ${src}_${cmd} "$@"
            local ecode=$?
            if [ $ecode = 127 ]; then
                echo "Unknown command: $(Bad $cmd) (Exit code $ecode)." >&2
            fi
            ;;
    esac
}
