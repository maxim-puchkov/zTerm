#!/bin/zsh

#  fsize
#  Z-Function
#
#  Created by Maxim Puchkov on 2020-04-25.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# Display sizes of files or directories.
function fsize() {
    trap 'unfunction -m "fsize_*"' EXIT
    # Reduce large KB sizes to MB or GB
    function fsize_simplify-kb() {
        local filesize=$1
        local unit='KB'
        if [[ $filesize -ge 1048576 ]]; then
            convert --var filesize 'kilobyte' 'gigabyte'
            unit='GB'
        elif [[ $filesize -ge 1024 ]]; then
            convert --var filesize 'kilobyte' 'megabyte'
            unit='MB'
        fi
        printf '%.2f %s' $filesize "$unit"
    }
    #
    local name type unit
    float -F 2 total_size size
    total_size=0
    for file in ${${@:-*}:a}; do
        size=0
        unit='KB'
        type=$(stat -f '%HT' $file)
        printf '%s: ' "$file"
        case $type in
            'Directory')
                du -k -d0 $file 2>/dev/null |
                read -r {size,name}
            ;;
            *)
                stat -f '%z %N' $file |
                read -r {size,name}
                convert --var size 'byte' 'kilobyte'
            ;;
        esac
        total_size=$(( $total_size + $size ))
        print -P "%F{4}$(fsize_simplify-kb $size)%f"
    done
    # Print total size when given more than one argument.
    if [[ $# -ne 1 ]]; then
        print -P "\n%BTotal size: %F{12}$(fsize_simplify-kb $total_size)%f%b"
    fi
    return 0
}
