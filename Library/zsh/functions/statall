#!/bin/zsh

#  statall
#  Z-Function (68)
#
#  Created by Maxim Puchkov on 2020-06-15.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# <#Function description#>
function statall() {
    # CSV Header
    typeset -a headers
    headers=(
        'Name' 'Type'   ' '
        'Hp' 'Mp' 'Lp' 'User' 'Group'   ' '
        'Disk' 'Size (bytes)' '# Links' '# Blocks' 'Block size' ' '
        'Device number' 'Major/Minor'       ' '
        'Access' 'Modification' 'Change'    ' '
        'i-node number' 'i-node birthtime' 'i-node gen. number'
    )
    # Format specifiers for 'stat'
    typeset -a format
    format=(
        SN  ST              't' # File
        OHp OMp OLp Su  Sg  't' # Access
        Sd  Dz  Dl  Db  Dk  't' # Disk
        r   Z               't' # Device (/dev)
        Sa  Sm  Sc          't' # Time
        i   SB  v               # i-node
    )
    format=('%'${^format})
    # Join arrays with commas
    local headers_str=${(j:,:)headers}
    local format_str=${(j:,:)format}
    # Print comma-separated file status fields to stdout
    print $headers_str >&1
    local file
    for file in $argv; do
        stat -L -f $format_str $file >&1
    done
    # Log number of files to stderr
    print -- "$0: analyzed ${#argv} files" >&2
    return 0
}
