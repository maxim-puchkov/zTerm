#!/bin/zsh

#  clr-xccolortheme
#  Z-Function (17)
#
#  Created by Maxim Puchkov on 2020-05-02.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# clr-xccolortheme
#   Change default labels in a CLR file (~/Library/Colors) to Xcode
#   color theme labels (Xcode Preferences/Fonts & Colors).
#
# Example:
#   CLR file: ~/Library/Colors/Unnamed.clr
#   Original labels:   Unnamed,     Unnamed 2, ..., Unnamed 27
#   Converted labels:  Plain Text,  Comment,   ..., Heading
function clr-xccolortheme() (
    local colors_clr placeholder_label labels_file labels tmp
    colors_clr="$1"                                         # .clr input file
    placeholder_label="${2:-${colors_clr%.clr}}"            # default label
    labels_file="${3:-$ZTERM/etc/XcodeColorTheme.labels}"   # Color theme labels
    labels=(${(@f)"$(< $labels_file)"})
    # Convert property list from CLR to XML format
    tmp=$( tempfile 'xml' )
    trap "rm -f $tmp" EXIT
    plutil -convert 'xml1' -o $tmp -- $colors_clr
    # Replace labels in XML
    local counter old_label new_label
    for counter in '' {2..${#labels}}; do
        old_label="<string>${placeholder_label} ?${counter}<\/string>"
        new_label="<string>${(Q)labels[${counter:-1}]}<\/string>"
        sed -E -i='' "s/$old_label/$new_label/g" $tmp
    done
    # Convert updated file back to CLR
    plutil -convert 'binary1' -o "new-$colors_clr" -- $tmp
    return 0
)
