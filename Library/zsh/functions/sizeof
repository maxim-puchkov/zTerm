#!/bin/zsh

#  sizeof
#  Z-Function (124)
#
#  Created by Maxim Puchkov on 2020-09-14.
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# sizeof - print human-readable size of files and directories
function sizeof() {(
  setopt noglob
  autoload -Uz error
  
  # Use absolute paths
  set -- ${${argv:=.}:a}
  
  # For each file or directory
  typeset -a reply=()
  local name
  local -i size
  for name; do
    # Directory
    if [[ -d $name ]]; then
      let size=$(du -Ls $name 2>/dev/null | cut -d $'\t' -f 1)*${BLOCKSIZE:-1}
    # Regular file
    elif [[ -r $name ]]; then
      let size=$(stat -f '%z' $name 2>/dev/null)
    # Missing file
    else
      error +R 'File not found: ${name}.'
      continue
    fi
    
    # Simplify size and append to reply
    local simplified_size="$(simplify-size $size)"
    if [[ -t 1 ]]; then
      simplified_size="%F{6}${simplified_size}%f"
    fi
    reply+=($name $simplified_size)
  done
  
  # Print sizes
  if [[ -z $reply ]]; then
    #error -1 'Unable to determine file size.'
    return 1
  fi
  
  if [[ $# -gt 1 ]]; then
    builtin print -aC2 -- ${(%)reply}
  else
    builtin print -- ${(%)reply[2]}
  fi
  return 0
)}
