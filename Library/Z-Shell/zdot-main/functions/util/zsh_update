#!/bin/zsh

#  zsh_update
#  zTerm (id: 139)
#
#  Generated by 'zfn' on 2020-11-05
#  Copyright Â© 2020 Maxim Puchkov. All rights reserved.


# zsh_update
# preexec
#
# Update autoload functions that were modified
# while the terminal window is open.
emulate -LR zsh


# Last update file for current TTY.
local last_update="/tmp/${TTY##*/}_update"
if [[ ! -e $last_update ]]; then
  /usr/bin/touch -- $last_update
  return 0
fi


# Set update directories.
if [[ ! -v update_dirs ]]; then
  typeset -a update_dirs=($ZDOTDIR/functions)
fi

# Find all updated functions.
local -a updated_files
updated_files=(${(@f)"$(
  /usr/bin/find --         \
      $update_dirs         \
      -type f              \
      -mnewer $last_update \
      2>/dev/null
)"})

# Unset and re-autoload functions.
local file name
for file in ${updated_files[@]}; do
  name=${file:t}
  if [[ ${+functions[$name]} -eq 1 ]]; then
    unset -f -- $name
  fi
  autoload -Uz -- $name
done

# If functions were updated, touch last update.
if [[ ${#updated_files} -gt 0 ]]; then
  /usr/bin/touch -- $last_update
  return 0
fi

