#!/bin/zsh

#  styles
#  /usr/local/zterm/Library/Z-Shell/sources/styles
#
#  User-specific profile for interactive zsh(1) shells.
#  Created on October 11, 2020.


# Add 'dim' and 'no-dim' text styles
FX[dim]="%{\033[2m%}"
FX[no-dim]="%{\033[22m%}"



# add-color-functions
# Define functions for printing colored text.
() {
  local color_name
  local -a exclude=(default)
  for color_name in ${(k)fg:|exclude}; do
    function "$color_name"() {
      print -r -- "${fg[$0]}${argv}${fg[default]}"
      return $(( $# == 0 ))
    }
  done
}


# print-line
# Print text with full-line width background color.
function print-line() {
  print -- "${(%)BG[252]}${(r:$COLUMNS:):- $argv}\e[49m"
}






function nocolor() {
  printf '%s\n' "${@//$'\033['[0-9;]#m/}"
}

function clr() {
  printf '%b%b%b' '\033[H' '\033[2J' '\033[3J'
}
